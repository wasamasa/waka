;; lexer

whitespace = "[[:space:]|]+" .
comment = "#.*" .
sexp = "(...)" . ; use the read procedure
token = "[^[:space:]|;(]+" .

;; parser

score = track+ .
track = name sequence .
name = instrument [ ":" nickname ] ":" .
instrument = identifier .
nickname = identifier .
identifier = alnum+ .
sequence = item+ .
item = chord | rest | octave | shift | sexp .
chord = note { "/" note } .
note = { shift } key { modifier } .
key = "a" | "b" | "c" | "d" | "e" | "f" | "g" .
modifier = duration | accidentals | natural | dotted .
accidentals = accidental+ .
accidental = "+" | "-" .
natural = "_" .
dotted = "."+ .
rest = "r" [ duration ] .
octave = "o" digit .
shift = "<" | ">".
duration = digits { "~" digits } .
digits = digit+ .
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" .
alnum = upper | lower | digit .
upper = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" |
        "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" |
        "U" | "V" | "W" | "X" | "Y" | "Z" .
lower = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" |
        "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" |
        "u" | "v" | "w" | "x" | "y" | "z" .
